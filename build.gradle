// Apply the java plugin to add support for Java
apply plugin: 'java'

// Apply the application plugin to add support for building an application
apply plugin: 'application'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    ext.commonsVersion = '1.3.2'
    ext.mailVersion = '1.6.0'
    ext.junitVersion = '4.12'

    compile "org.apache.commons:commons-io:$commonsVersion"
    compile "javax.mail:javax.mail-api:$mailVersion"

    // Use JUnit test framework.
    testCompile "junit:junit:$junitVersion"
}

version = '2.1'

// Define the main class for the application
mainClassName = 'com.github.friedrichfroebel.occar.Gui'

// Default build does not include the needed libraries.
// Therefore the jar option should be used for releases.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes (
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

// Check code style.
apply plugin: 'checkstyle'
checkstyle {
    toolVersion '8.1'
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/checkstyle/checkstyle.xsl')
    }
}

// Use the PMD plugin.
apply plugin: 'pmd'
tasks.withType(Pmd) {
    ignoreFailures = true
    consoleOutput = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
}

// Update the version files.
task versionFiles {
    def fileJava = new File('src/main/java/com/github/friedrichfroebel/occar/config/Version.java')
    fileJava.createNewFile()
    fileJava.text = "package com.github.friedrichfroebel.occar.config;\n\n" +
            "public final class Version {\n\n" +
            "    public static final String VERSION = \"$version\";\n\n}"

    def fileRoot = new File('version')
    fileRoot.createNewFile()
    fileRoot.text = "$version"
}
tasks.getByName('run').dependsOn versionFiles
tasks.getByName('jar').dependsOn versionFiles
