// Apply the versions plugin to check for module dependency updates.
plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
}

// Apply the java plugin to add support for Java
apply plugin: 'java'

// Apply the application plugin to add support for building an application
apply plugin: 'application'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    ext.commonsVersion = '1.3.2'
    ext.mailVersion = '1.6.2'
    ext.jsonVersion = '20190722'
    ext.junitVersion = '4.13'

    implementation "org.apache.commons:commons-io:$commonsVersion"
    implementation "javax.mail:javax.mail-api:$mailVersion"
    implementation "org.json:json:$jsonVersion"

    // Use JUnit test framework.
    testImplementation "junit:junit:$junitVersion"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation'
    options.deprecation = true
}

version = '2.2'

// Define the main class for the application
mainClassName = 'com.github.friedrichfroebel.occar.Gui'

// Target at least Java 8.
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Default build does not include the needed libraries.
// Therefore the jar option should be used for releases.
jar {
    from configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes (
                'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

// Check code style.
apply plugin: 'checkstyle'
checkstyle {
    toolVersion '8.31'
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/checkstyle/checkstyle.xsl')
    }
}

// Use the PMD plugin.
apply plugin: 'pmd'
tasks.withType(Pmd) {
    ignoreFailures = true
    consoleOutput = true
}

wrapper {
    gradleVersion = '6.3'
}

// Update the version files.
task versionFiles {
    def fileJava = new File('src/main/java/com/github/friedrichfroebel/occar/config/Version.java')
    fileJava.createNewFile()
    fileJava.text = "package com.github.friedrichfroebel.occar.config;\n\n" +
            "public final class Version {\n\n" +
            "    public static final String VERSION = \"$version\";\n\n}"

    def fileRoot = new File('version')
    fileRoot.createNewFile()
    fileRoot.text = "$version"
}
tasks.getByName('run').dependsOn versionFiles
tasks.getByName('jar').dependsOn versionFiles
